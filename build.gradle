plugins {
    id 'net.minecraftforge.gradle' version '5.1.+'
    id "com.modrinth.minotaur" version "2.+"
    id "net.darkhax.curseforgegradle" version "1.0.11"
}

version = project.hasProperty('release') ? project.getProperty('release') : 'dev'
group = 'gs.mclogs.forge'
archivesBaseName = 'mclogs-forge'
changelog = rootProject.file("CHANGELOG.md").text
java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
repositories {
    mavenCentral()
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.15.2-31.2.50'

    include project(path: ":mclogs-java", configuration: 'default')
}

minecraft {
    mappings channel: 'snapshot', version: '20200514-1.15.1'
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                mclogs {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                mclogs {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'mclogs', '--all', '--output', file('src/generated/resources/')
            mods {
                mclogs {
                    source sourceSets.main
                }
            }
        }
    }
}

jar {
    manifest {
        attributes([
            "Specification-Title": "mclogs",
            "Specification-Vendor": "Aternos",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"Aternos",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    from configurations.include.collect { it.isDirectory() ? it : zipTree(it)}
}
jar.finalizedBy('reobfJar')

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "6DdCzpTL"
    uploadFile = "./build/libs/mclogs-forge-${project.version}.jar"
    gameVersions = [
            "1.15.2",
            "1.15.1",
            "1.15",
            "1.14.4",
            "1.14.3",
            "1.14.2",
            "1.14.1",
            "1.14",
    ]
    changelog = rootProject.changelog
}

task publishCurseforge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    apiToken = System.getenv("CURSEFORGE_TOKEN") ?: ''
    def mainFile = upload(420561, "./build/libs/mclogs-forge-${project.version}.jar")
    mainFile.releaseType = 'release'
    mainFile.changelogType = 'markdown'
    mainFile.changelog = rootProject.changelog
    mainFile.addJavaVersion("Java ${JavaVersion.current().majorVersion}")
    mainFile.addGameVersion("1.15.2")
    mainFile.addGameVersion("1.15.1")
    mainFile.addGameVersion("1.15")
    mainFile.addGameVersion("1.14.4")
    mainFile.addGameVersion("1.14.3")
    mainFile.addGameVersion("1.14.2")
    mainFile.addGameVersion("1.14.1")
    mainFile.addGameVersion("1.14")
}
